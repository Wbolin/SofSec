MODULE user(semaphore, active)
VAR
  state : { idle, entering, critical, exiting };
ASSIGN
  init(state) := idle;
  next(state) := case
    state = idle & active : { idle, entering };
    state = entering & !semaphore & active: critical;
    state = critical & active: { critical, exiting };
    state = exiting & active: idle;
    TRUE : state;
  esac;

  FAIRNESS
    state = entering;

MODULE main
VAR
  semaphore : boolean;
  pid : 0..1;
  proc1 : user(semaphore, next(pid) = 0);
  proc2 : user(semaphore, next(pid) = 1);
ASSIGN
  init(semaphore) := FALSE;

  next(semaphore) := case
    proc1.state = entering & next(pid) = 0 & !semaphore: TRUE;
    proc2.state = entering & next(pid) = 1 & !semaphore: TRUE;
    proc1.state = exiting & pid = 0 & semaphore: FALSE;
    proc2.state = exiting & pid = 1 & semaphore: FALSE;
    TRUE : semaphore;
  esac;

SPEC AG !(proc1.state = critical & proc2.state = critical); -- safety

SPEC AG (proc1.state = entering -> AF proc1.state = critical); -- liveness
